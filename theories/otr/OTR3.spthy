theory OTR3
begin

builtins: diffie-hellman, revealing-signing

rule GenKey:
    [Fr(~sk)]
    --[Gen($A, ~sk)]->
    [!Ltk($A, ~sk), !Pk($A, pk(~sk)), Out(pk(~sk))]

rule Rev:
    [!Ltk(A, sk)]
    --[Reveal(A)]->
    [Out(sk)]

rule A_init:
    [Fr(~id), !Ltk(A, skA), !Pk(B, pkB)]
    --[Create_I(A, ~id)]->
    [St_I_1(A, B, ~id, skA, pkB)]

rule B_init:
    [Fr(~id), !Ltk(B, skB), !Pk(A, pkA)]
    --[Create_R(B, ~id)]->
    [St_R_1(B, A, ~id, skB, pkA)]

rule A_send1:
    [Fr(~x), St_I_1(A, B, id, skA, pkB)]
    -->
    [St_I_2(A, B, id, skA, pkB, ~x), Out(<revealSign(<'g'^~x, B>, skA), pk(skA)>)]

rule B_recv1:
    let s = getMessage(t) 
        gx = fst(s)
    in 
    [In(<t, pkA>), St_R_1(B, A, id, skB, pkA)]
    --[Eq(revealVerify(t, <gx, B>, pkA), true)]->
    [St_R_2(B, A, id, skB, pkA, gx)]

rule B_send2:
    [Fr(~y), St_R_2(B, A, id, skB, pkA, gx)]
    --[Running(B, A, <'I', 'R', gx^~y>), Sent(pk(skB))]->
    [St_R_3(B, A, id, skB, pkA, gx, ~y), Out(<revealSign(<'g'^~y, A>, skB), pk(skB)>)]

rule A_recv2:
    let s = getMessage(t)
        gy = fst(s)
    in
    [In(<t, pkB>), St_I_2(A, B, id, skA, pkB, x)]
    --[Commit(A, B, <'I', 'R', gy^x>), Eq(revealVerify(t, <gy, A>, pkB), true), Finish(), Recv(pkB)]->
    [St_I_3(A, B, id, skA, pkB, x, getMessage(t))]

restriction Equality:
    "All m1 m2 #i. Eq(m1, m2)@i ==> m1=m2" 

restriction oneKey:
    "All A sk1 sk2 #i #j. (Gen(A, sk1)@i & Gen(A, sk2)@j) ==> sk1 = sk2"
// Verified
lemma Executability:
    exists-trace "Ex #i. Finish() @i &  not (Ex A #j. Reveal(A)@j)
    & (All y #i. Recv(y)@i ==> Ex #j. Sent(y)@j)
    & (All A B id1 id2 #i #j. (Create_R(A, id1)@i & Create_R(B, id2)@j) ==> id1 = id2)
    & (All A B id1 id2 #i #j. (Create_I(A, id1)@i & Create_I(B, id2)@j) ==> id1 = id2)
    & (All A id1 #i. Create_I(A, id1)@i ==> not (Ex id2 #j. Create_R(A, id2)@j))"

// Falsified
lemma noninjectiveagreementINITIATOR:
    "All a b t #i.
        Commit(a,b,<'I','R',t>)@i &
        not ((Ex #r. Reveal(a)@r) | (Ex #r. Reveal(b)@r))
        ==> (Ex #j. Running(b,a,<'I','R',t>)@j)"

end